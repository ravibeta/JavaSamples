/*
 * Copyright 2013-2018 Dell Inc. or its subsidiaries. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 * http://www.apache.org/licenses/LICENSE-2.0.txt
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */
group = 'com.dellemc.connector'
version = '1.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: "jacoco"

repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://repository.apache.org/snapshots"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.24'
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.24'
    compile group: 'com.jayway.jsonpath', name:'json-path', version:'2.1.0'
    compile group: 'org.springframework', name: 'spring-web', version: '5.0.2.RELEASE'
    compile(group: 'io.pravega', name: 'pravega-client', version: pravegaVersion) {
        exclude group:  'org.slf4j', module: 'slf4j-api'

        // lombok should be compileOnly
        exclude group: 'org.projectlombok', module: 'lombok'

        // pravega-shared pulls in curator which isn't needed on the client
        exclude group: 'org.apache.curator', module: 'curator-recipes'
    }
    testCompile "junit:junit:4.+"
}

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/jacoco")
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

jar {
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes(
                'Main-Class': 'com.dellemc.connector.tcp.Connector'
        )
    }
}


test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        // classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}


jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
        html.destination file("${buildDir}/jacoco/jacocoHtml")
    }
}
